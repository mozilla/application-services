buildscript {
    // in mozilla-central there's no build.gradle suitable for the maven/glean config, so each component gets it.
    if (gradle.hasProperty("mozconfig")) {
        repositories {
            gradle.mozconfig.substs.GRADLE_MAVEN_REPOSITORIES.each { repository ->
                maven {
                    url = repository
                    if (gradle.mozconfig.substs.ALLOW_INSECURE_GRADLE_REPOSITORIES) {
                        allowInsecureProtocol = true
                    }
                }
            }
        }

        dependencies {
            classpath libs.glean.gradle.plugin
        }
    }
}

plugins {
    alias libs.plugins.python.envs.plugin
}

apply from: "$appServicesRootDir/build-scripts/component-common.gradle"
apply from: "$appServicesRootDir/publish.gradle"

ext {
    gleanNamespace = "mozilla.telemetry.glean"
    gleanYamlFiles = ["${project.projectDir}/../metrics.yaml"]
    if (gradle.hasProperty("mozconfig")) {
        gleanPythonEnvDir = gradle.mozconfig.substs.GRADLE_GLEAN_PARSER_VENV
    }
}
apply plugin: "org.mozilla.telemetry.glean-gradle-plugin"

android {
    namespace 'org.mozilla.experiments.nimbus'
}

dependencies {
    api project(":remotesettings")

    implementation libs.androidx.core
    implementation libs.androidx.annotation
    implementation libs.kotlin.coroutines
    implementation libs.mozilla.glean

    testImplementation libs.mozilla.glean.forUnitTests
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.junit
    testImplementation libs.androidx.work.testing
}

ext.configureUniFFIBindgen("nimbus")
ext.dependsOnTheMegazord()
ext.configurePublish()
